buildscript {
  repositories {
    mavenCentral()
  }
  
  dependencies {
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
  }
}

plugins {
  id 'java'
  id 'war'
  id 'com.eriwen.gradle.css'
  id 'com.eriwen.gradle.js'
}

apply plugin: 'com.google.cloud.tools.appengine-appyaml'


repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  providedCompile 'org.eclipse.jetty:jetty-server:11.0.+'
  providedCompile 'org.eclipse.jetty:jetty-annotations:11.0.+'
  providedCompile 'org.eclipse.jetty:apache-jsp:11.0.+'
  
  implementation 'org.eclipse.jetty:apache-jstl:11.0.+'
  implementation 'commons-codec:commons-codec:1.15'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.+'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.13.+'
  implementation 'com.udojava:EvalEx:2.+'
  
  implementation 'com.github.mifmi:mifmi-commons4j:v0.3.+'
  implementation 'com.github.mozq:enigma4j:v1.+'
  
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

appengine {
  deploy {
    projectId 'GCLOUD_CONFIG'
    version 'GCLOUD_CONFIG'
    promote false
    stopPreviousVersion false
  }
}

combineCss {
  source "${webAppDirName}/static/css/main.css"
  dest "${buildDir}/minify/css/all.css"
}

minifyCss {
  source combineCss
  dest "${buildDir}/minify/css/all.min.css"
}

combineJs {
  encoding 'UTF-8'
  source "${webAppDirName}/static/js/analytics.js"
  source "${webAppDirName}/static/js/core.js"
  source "${webAppDirName}/static/js/main.js"
  source "${webAppDirName}/static/js/adsense.js"
  dest "${buildDir}/minify/js/all.js"
}

minifyJs {
  source combineJs
  dest "${buildDir}/minify/js/all.min.js"
  sourceMap "${buildDir}/minify/js/all.sourcemap.json"
  closure {
    compilerOptions.languageOut = 'ECMASCRIPT5'
  }
}

war {
  dependsOn minifyCss, minifyJs
  
  webInf {
    from "${buildDir}/minify/css/"
    into "/static/css/"
  }
  
  webInf {
    from "${buildDir}/minify/js/"
    into "/static/js/"
  }
}

jar {
  manifest {
    attributes 'Main-Class': 'com.dencode.web.server.ServerMain'
  }
}

task copyJar(type: Copy) {
  dependsOn jar
  
  from project.libsDirectory
  include '*.jar'
  into appengine.stage.stagingDirectory
}

task copyDependencies(type: Copy) {
  from configurations.providedCompile
  into appengine.stage.stagingDirectory
}

appengineStage.finalizedBy copyJar, copyDependencies

task appRunStage(type: JavaExec) {
  dependsOn appengineStage
  
  environment PORT: 8080
  workingDir appengine.stage.stagingDirectory
  classpath "${appengine.stage.stagingDirectory}/*"
  main 'com.dencode.web.server.ServerMain'
}

group = 'com.dencode'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11
